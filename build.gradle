buildscript {
    repositories {
        gradlePluginPortal()
        maven { url 'https://palantir.bintray.com/releases' }
    }
    dependencies {
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:1.1.0'
        classpath 'com.netflix.nebula:gradle-info-plugin:9.3.0'
        classpath 'com.netflix.nebula:nebula-publishing-plugin:17.3.2'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.3.0'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.8.5'
    id 'com.gradle.plugin-publish' version '0.13.0'
    id 'com.palantir.git-version' version '0.12.3'
    id 'com.palantir.idea-test-fix' version '0.1.0'
    id 'com.palantir.baseline' version '3.75.0'
    id 'com.palantir.consistent-versions' version '1.28.0'
    id 'java-gradle-plugin'
    id 'groovy'
}

apply from: 'gradle/publish-jar.gradle'

group 'com.palantir.gradle.gitversion'
version System.env.CIRCLE_TAG ?: gitVersion()

repositories {
    jcenter()
    maven { url 'https://palantir.bintray.com/releases' }
}

sourceSets {
    groovy
    main.output.dir groovy.output
}

dependencies {
    implementation gradleApi()
    implementation 'org.eclipse.jgit:org.eclipse.jgit'
    implementation 'com.google.guava:guava'

    groovyImplementation localGroovy()
    groovyImplementation gradleApi()

    // Access code written in groovy from src/main/java sources
    runtimeOnly configurations.groovyRuntimeClasspath
    implementation configurations.groovyCompile
    implementation sourceSets.groovy.output

    annotationProcessor 'org.immutables:value'
    compileOnly 'org.immutables:value::annotations'

    testImplementation gradleTestKit()
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine") {
        because 'allows JUnit 3 and JUnit 4 tests to run'
    }
    testImplementation 'org.assertj:assertj-core'
    testImplementation('org.spockframework:spock-core') {
        exclude module: 'groovy-all'
    }
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
}

java {
    sourceCompatibility JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Werror', '-Xlint:deprecation']
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-git-version'
    vcsUrl = 'https://github.com/palantir/gradle-git-version'
    description = 'Gradle Git-Version is a plugin that generates a version for use with Gradle by calling git-describe.'
    tags = ['git', 'version']

    plugins {
        gitVersionPlugin {
            id = 'com.palantir.git-version'
            displayName = 'Palantir Gradle Git-Version'
        }
    }
allprojects {
    apply plugin: 'com.palantir.java-format'
}
}

// Configure the publishPlugins task
publishPlugins.onlyIf { System.env.CIRCLE_TAG }
tasks.publish.dependsOn publishPlugins
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]
