buildscript {
    repositories {
        gradlePluginPortal()
        maven { url 'https://palantir.bintray.com/releases' }
    }
    dependencies {
        classpath 'com.netflix.nebula:nebula-publishing-plugin:14.1.1'
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.gradle.plugin-publish' version '0.10.1'
    id 'com.palantir.git-version' version '0.12.2'
    id 'com.palantir.idea-test-fix' version '0.1.0'
    id 'com.palantir.baseline' version '2.33.0'
    id 'com.palantir.consistent-versions' version '1.12.4'
    id 'java-gradle-plugin'
    id 'groovy'
}

apply from: 'gradle/publish-jar.gradle'

group 'com.palantir.gradle.gitversion'
version gitVersion()

repositories {
    jcenter()
    maven { url 'https://palantir.bintray.com/releases' }
}

dependencies {
    compile gradleApi()
    compile 'org.eclipse.jgit:org.eclipse.jgit'
    compile 'com.google.guava:guava'

    testCompile gradleTestKit()
    testCompile 'junit:junit'
    testCompile 'org.assertj:assertj-core'
    testCompile('org.spockframework:spock-core') {
        exclude module: 'groovy-all'
    }
}

java {
    sourceCompatibility JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
    options.compilerArgs += ['-Werror', '-Xlint:deprecation']
}

pluginBundle {
    website = 'https://github.com/palantir/gradle-git-version'
    vcsUrl = 'https://github.com/palantir/gradle-git-version'
    description = 'Gradle Git-Version is a plugin that generates a version for use with Gradle by calling git-describe.'
    tags = ['git', 'version']

    plugins {
        gitVersionPlugin {
            id = 'com.palantir.git-version'
            displayName = 'Palantir Gradle Git-Version'
        }
    }
}

// Configure the publishPlugins task
publishPlugins.onlyIf { System.env.CIRCLE_TAG }
tasks.publish.dependsOn publishPlugins
project.ext.'gradle.publish.key' = System.env["GRADLE_KEY"]
project.ext.'gradle.publish.secret' = System.env["GRADLE_SECRET"]
